## Summary

### Findings

|              | Issue                                                                                             |
| ------------ | ------------------------------------------------------------------------------------------------- |
| [H-01](#H01) | If debt token has a blacklist (like USDC / tether / ...), then repay() does not work for borrower |
| [H-02](#H02) | Not checking return value for ERC20 transferFrom and transfer                                     |
| [M-01](#M01) | Missing check for decollateralized > 0 in Cooler.repay                                            |
| [L-01](#L01) | ClearingHouse can only handle tokens with 18 decimals                                             |
| [L-02](#L02) | Hardcoded values in ClearingHouse can lead to a bad state                                         |
| [L-03](#L03) | Not scaling tokens depending on their decimals will lead to wrong calculations                    |
| [L-04](#L04) | Not checking for loan.lender != msg.sender in repay function                                      |
| [L-05](#L05) | DOS CoolerFactory.newEvent and Request[] public requests                                          |
| [L-06](#L06) | Possible re-entrency attack in Cooler if deployed with malicious factory                          |
| [L-07](#L07) | Missing important functions for protocol front-end                                                |

---

## High

### <a id="H01"></a> H-01 If debt token has a blacklist (like USDC / tether / ...), then repay() does not work for borrower
`Cooler.repay` sends the debt token to the lender, if the debt token has implemented a blacklist (like USDC, Tether, ...) and the lender is added to the blacklist of that token it's not possible for the borrower to repay his debt.

#### Vulnerability Detail
Consider the debt token is USDC and a borrower wants to repay his debt, he needs to use the repay function.

```solidity
function repay (uint256 loanID, uint256 repaid) external {
    Loan storage loan = loans[loanID];

    if (block.timestamp > loan.expiry) 
        revert Default();
    
    uint256 decollateralized = loan.collateral * repaid / loan.amount;

    if (repaid == loan.amount) delete loans[loanID];
    else {
        loan.amount -= repaid;
        loan.collateral -= decollateralized;
    }

    debt.transferFrom(msg.sender, loan.lender, repaid);
    collateral.transfer(owner, decollateralized);
}
```

The repay functions transfers the repaid amount directly to the lender via
`debt.transferFrom(msg.sender, loan.lender, repaid);`

Now if the lender was added to the backlist of USDC because of a malicious behaviour (interacting with tornado or anything) the transfer will fail and so the complete function. The borrower now has no chance to ever pay back his debt.

#### Impact
If the debt token has a blacklist check for token transfers and the lender is added to that blacklist, the borrower can't repay his debt and will loose all his collateral tokens.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L108-L124

#### Tool used

Manual Review

#### Recommendation
Instead of sending tokens directly to the lender, consider storing it in a variable where the lender can withdraw it via a withdraw function.

### <a id="H02"></a> H-02 Not checking return value for ERC20 transferFrom and transfer
Not checking the result for transferFrom or transfer can lead to lost of funds if `collateral` or `debt` token is not reverting but returning false in the function.

#### Vulnerability Detail
A call to transferFrom or transfer is frequently done without checking the results. For certain ERC20 tokens, if insufficient tokens are present, no revert occurs but a result of "false" is returned. So its important to check this and if it's false revert.

```solidity
File: src/Cooler.sol
85:         collateral.transferFrom(msg.sender, address(this), collateralFor(amount, loanToCollateral)); 

File: src/Cooler.sol
102:         collateral.transfer(owner, collateralFor(req.amount, req.loanToCollateral));

File: src/Cooler.sol
122:         debt.transferFrom(msg.sender, loan.lender, repaid);
123:         collateral.transfer(owner, decollateralized);

File: src/Cooler.sol
146:         collateral.transferFrom(msg.sender, address(this), newCollateral);

File: src/Cooler.sol
179:         debt.transferFrom(msg.sender, owner, req.amount);

File: src/Cooler.sol
205:         collateral.transfer(loan.lender, loan.collateral);
```

An example implementation for tokens that return false instead of reverting.
```solidity
    function transfer(address _to, uint _value) returns (bool) {
        //Default assumes totalSupply can't be over max (2^256 - 1).
        if (balances[msg.sender] >= _value && balances[_to] + _value >= balances[_to]) {
            balances[msg.sender] -= _value;
            balances[_to] += _value;
            Transfer(msg.sender, _to, _value);
            return true;
        } else { return false; }
    }

    function transferFrom(address _from, address _to, uint _value) returns (bool) {
        if (balances[_from] >= _value && allowed[_from][msg.sender] >= _value && balances[_to] + _value >= balances[_to]) {
            balances[_to] += _value;
            balances[_from] -= _value;
            allowed[_from][msg.sender] -= _value;
            Transfer(_from, _to, _value);
            return true;
        } else { return false; }
    }
```

#### Impact
Impact can be critical for both, the lender as well as the borrower and depending on which of the tokens is not reverting but returning false.

For example if the collateral is one of this tokens, borrower can open a valid request without paying the tokens. As soon as a lender is accepting the request he will loan out tokens without a colleteral.

If the debt token is not reverting, an lender can accept any Request, the requested amount will not be sent to the borrower but the collateral will be gone for him and the lender can claim them after the period is over.

As the `CoolerFactory` and `Cooler` are immutable and can create any collateral-debt pair, it should implement the checks.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L85
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L102
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L122
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L123
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L146
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L179
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L205

#### Tool used
Manual Review

#### Recommendation
Check the result of transfer and transferFrom or use the SafeERC20 library from OpenZeppelin and use safeTransfer and safeTransferFrom.

---

## Medium

### <a id="M01"></a> M-01 Missing check for decollateralized > 0 in Cooler.repay
Missing check in `Cooler.repay` can bring ratio for loan.amount and loan.collateral out of sync and lead to lost of funds for borrower and stucked collateral in Cooler contract.

#### Vulnerability Detail
The current implementation in `Cooler.repay` calculates the `decollateralized` that will be transfered back to the borrower and repays the lender the amount.
```solidity
File: src/Cooler.sol
114:         uint256 decollateralized = loan.collateral * repaid / loan.amount;
```

The problem is, that depending on the current loan.collateral and loan.amount the value can be 0 and so the loan.amount will reduce, but the loan.collateral will stay the same.

Whenever `loan.collateral * repaid < loan.amount` the borrower will pay the amount for `repaid` to the lender, but will never get back any collateral as it will always result in 0.

#### Impact
If borrower uses repay but is not paying enough, he will loose collateral as he will not get back any tokens for his payment. This can also lead to unexpected behaviour for `Cooler.roll` as the ratio for loan.amount and loan.collateral is out of sync.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L114
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L122-L123

#### Tool used
Manual Review

#### Recommendation
Implement a check for `decollateralized > 0` so it's not possible for a borrower to repay not enough tokens to break the formular.

https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L108-L124
```solidity
function repay (uint256 loanID, uint256 repaid) external {
    Loan storage loan = loans[loanID];

    if (block.timestamp > loan.expiry) 
        revert Default();
    
    uint256 decollateralized = loan.collateral * repaid / loan.amount;
    
    // @audit add check here, so it's not possible to have decollateralized == 0
    require(decollateralized > 0, "not enough repaid");

    if (repaid == loan.amount) delete loans[loanID];
    else {
        loan.amount -= repaid;
        loan.collateral -= decollateralized;
    }

    debt.transferFrom(msg.sender, loan.lender, repaid);
    collateral.transfer(owner, decollateralized);
}
```

---

## Low

### <a id="L01"></a> L-01 ClearingHouse can only handle tokens with 18 decimals
ClearingHouse can only handle tokens with 18 decimals. Currently it's only possible to deploy token-pairs where both tokens have 18 decimals. A usdc-gOHM pair is not possible at the moment.

#### Vulnerability Detail
Currently the ClearingHouse is using hardcoded values to check if they can accept a loan via `ClearingHouse.clear` or not.

```solidity
File: src/aux/ClearingHouse.sol
33:     uint256 public constant minimumInterest = 2e16; // 2%
34:     uint256 public constant maxLTC = 2_500 * 1e18; // 2,500
```

If the protocol ever decides to also accept USDC-gOHM for example the current implementation for ClearingHouse can't be used as they don't scale depending on the decimals from the tokens.

#### Impact
If protocol wants to use a ClearingHouse with a token-pair that is not both 18 decimals the currently fixed values will break the expectation as they're not changeable and not scaled depending on the decimals of the tokens.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/aux/ClearingHouse.sol#L33-L34
https://github.com/ohmzeus/Cooler/blob/main/src/aux/ClearingHouse.sol#L76-L79

#### Tool used
Manual Review

#### Recommendation
Use immutable variables for `minimumInterest` and `maxLTC` that you can set on the deployment or scale the values depending on the decimals for the tokens.

### <a id="L02"></a> L-02 Hardcoded values in ClearingHouse can lead to a bad state
Hardcoded values in ClearingHouse prevent adjustments to changing market conditions and bring contract in a bad state.

#### Vulnerability Detail
`ClearingHouse.clear` is using constant values to decide if it should accept a lona request from a Cooler or not.

```solidity
File: src/aux/ClearingHouse.sol
33:     uint256 public constant minimumInterest = 2e16; // 2%
34:     uint256 public constant maxLTC = 2_500 * 1e18; // 2,500
35:     uint256 public constant maxDuration = 365 days; // 1 year
```

If market conditions change, it's not possible to adapt to make it more defensive or aggresive depending on the current market.

This will require a re-deployment of a new ClearingHouse and changing the values in the source-code before deployment, what can lead to errors and missconfigurations.

#### Impact
If market conditions change and the protocol want to accept interesets != 2% or more or less LTC it's not possible and require a redeployment and changing the source-code.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/aux/ClearingHouse.sol#L33-L35

#### Tool used
Manual Review

#### Recommendation
Consider using state variables and implement functions to update the values. With this, you can react and adapt the conditions depending on the current market.
If it is required that the values stay fixed, use immutable variables and set them in the constructor in the deployment, so you don't need to change the source code.

### <a id="L03"></a> L-03 Not scaling tokens depending on their decimals will lead to wrong calculations
If one of the tokens (collateral / debt) in `Cooler` is not 18 decimals the calculations in `Cooler.collateralFor` and `Cooler.interestFor` are wrong and lead to wrong token transfers.

#### Vulnerability Detail

`Cooler` is using a fixed scale of **1e18** for calculations inside the functions **collateralFor** and **interestFor**. This will lead to problems, if one of the tokens is not using 18 decimals.

Example:
A Cooler is created with gOHM (collateral with 18 decimals) and USDC (debt with 6 decimals).
> https://ag0.gitbook.io/cooler-loan-code-walkthrough/borrower-flow#requesting-a-loan
For example, if loan amount is 1000 and loan to collateral is 2000 DAI per gOHM, the math is: 1000 / 2000 = 0.5.

The `Cooler.request` will then be created with `amount = 1000000000` (6 decimals for USDC = 1000 * 1e6) and `loanToCollateral = 2000000000000000000000` (18 decimals for gOHM = 2000 * 1e18).
```solidity
File: src/Cooler.sol
236:     function collateralFor(uint256 amount, uint256 loanToCollateral) public pure returns (uint256) {
236:         return amount * decimals / loanToCollateral;
236:     }
```

We expect now that 5000000000000000000 gOHM (0.5 gOHM) will be transfered from the borrower as collateral for the requested 1000 USDC, but the current implementation transfers 500000 gOHM (0,0000000000005).

The borrower has opened a Request for 1000 USDC with only 0,0000000000005 gOHM as collateral instead of 0.5 gOHM. Lenders that now accept the request will be in risk, as the collateral is wrong.

If it's the other way around with collateral = 6 decimals and debt = 18 decimals the amount that will be transfered from the borrower as collateral will be > 10**12 times bigger than it should and if it's picked up instantly by a lender the borrower will have lost that.

#### Impact
If one of the tokens (collateral / debt) will have other than 18 decimals the calculation is off and either the borrower or the lender will loose money depending which of the tokens is different to 18 decimals.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L236-L238
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L245-L248

#### Tool used

Manual Review

#### Recommendation
Scale the tokens to match 18 decimals before calculation.

Example:
```solidity
uint256 private constant decimals = 1e18;
uint256 private immutable debtScale;
uint256 private immutable collatScale;

constructor (address o, ERC20 c, ERC20 d) {
    owner = o;
    collateral = c;
    collatScale = 10 ** (18 - c.decimals());
    debt = d;
    debtScale = 10 ** (18 - d.decimals());
    factory = CoolerFactory(msg.sender);
}

function collateralFor(uint256 amount, uint256 loanToCollateral) public view returns (uint256) {
    return (amount * debtScale) * decimals / (loanToCollateral * collatScale);
}
```

Also adapt the calculation inside `interestFor` to scale the amounts accordingly to the decimals.

### <a id="L04"></a> L-04 Not checking for loan.lender != msg.sender in repay function
Missing check `msg.sender != loan.lender` in `Cooler.repay` can lead to lost of funds for lender. If lender sending a transaction to that function he will loose his lended amount.

#### Vulnerability Detail

1. Attacker opens a request via `Cooler.request`
2. Victim accepts the offer via `Cooler.clear`
3. Before calling clear, victim approves unlimited `debt` tokens to be spent from Cooler.
4. Attacker get Victim to sign the repay function with repaid = loan.amount
5. Cooler will transfer the debt token to the lender as `msg.sender == loan.lender`
6. `collateral` will be transfered back to attacker

As the max approval in the beginning is "normal", the victim will not get any information when signing the repay transaction as it will look all normal.

```solidity
File: src/Cooler.sol
122:         debt.transferFrom(msg.sender, loan.lender, repaid);
```

#### Impact
Lender will loose all his lending amount to the attacker.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L122

#### Tool used
Manual Review

#### Recommendation
As there is no reason why the lender should ever call the repay function for the borrower you should implement a require to check that msg.sender is not lender.

Example
```solidity
function repay (uint256 loanID, uint256 repaid) external {
        Loan storage loan = loans[loanID];

        if(loan.lender == msg.sender) {
            revert NoRepayFromLender();
        }
```

Possible attack vectors for point 4 are for example an infected protocol frontend or in general any phising attack. Depending on the open positions and the popularity of the protocol the attack gets more and more attractive for an attacker as the missing "approval" for token spending is not needed to get the funds from the victim.

### <a id="L05"></a> L-05 DOS CoolerFactory.newEvent and Request[] public requests
An unchecked amount in `Cooler.request` can be used to DOS the protocol with Events via the `CoolerFactory.newEvent` function and unneccessery growing of the `Request[] public requests` variable.

#### Vulnerability Detail

Currently the `CoolerFactory.newEvent` function is protected to only accept calls from a valid deployed Cooler before an Event is emitted. The events are used in the protocol to consume off-chain and work with them and it's expected to only get valid events raised in the CoolerFactory contract.

```solidity
File: src/Factory.sol
42:     function newEvent (uint256 id, Events ev) external {
43:         require (created[msg.sender], "Only Created");
```

An attacker can deploy a valid Cooler via `CoolerFactory.generate` or just use any already deployed Cooler and then use the `Cooler.request` function to DOS the protocol.

```solidity
File: src/Cooler.sol
74:     function request (
75:         uint256 amount,
76:         uint256 interest,
77:         uint256 loanToCollateral,
78:         uint256 duration
79:     ) external returns (uint256 reqID) {
80:         reqID = requests.length;
81:         factory.newEvent(reqID, CoolerFactory.Events.Request);
82:         requests.push(
83:             Request(amount, interest, loanToCollateral, duration, true)
84:         );
85:         collateral.transferFrom(msg.sender, address(this), collateralFor(amount, loanToCollateral)); 
86:     }
```

The attacker can just call request with `amount = 0` and `loanToCollateral = 1`. This will always result in `collateralFor(amount, loanToCollateral) = 0` and so the attacker can grow the length of `requests` and doesn't need any tokens for the transferFrom function and can DOS the system without any capital.

```solidity
File: src/Cooler.sol
253:     function collateralFor(uint256 amount, uint256 loanToCollateral) public pure returns (uint256) {
254:         return amount * decimals / loanToCollateral;
255:     }
```
=> **0 * 1e18 / 1 = 0**

#### Impact
An attacker can DOS the protocol with unwanted events, depending on the chain it can be pretty cheap for him to DOS the protocol.

Raising it as high, because there is no way for the protocol to "blacklist" the attacker as he can just use every official Cooler to flood the events and also the attacker can just start growing the requests variable for every Cooler and so the front-end will be a mess with hundrets of valid 0 requests per Cooler.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Factory.sol#L43
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L74-L86
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L236-L238

#### Tool used
Manual Review

#### Recommendation
Implement an amount check in the `Cooler.request` function to prevent requesting valid Request with 0 amount.

Also consider adding `indexed` fields in the Events for better off-chain queries.

### <a id="L06"></a> L-06 Possible re-entrency attack in Cooler if deployed with malicious factory
If Cooler is deployed with a malicious factory it is vulnerable to re-entrency attacks.

#### Vulnerability Detail

At the moment Cooler creates events on the factory via the factory.newEvent call. The call is done before important state changes and can moved down in the functions to adhere to the check effect pattern.

```solidity
File: src/Cooler.sol
81:         factory.newEvent(reqID, CoolerFactory.Events.Request);
82:         requests.push(
83:             Request(amount, interest, loanToCollateral, duration, true)
84:         );

File: src/Cooler.sol
094:         factory.newEvent(reqID, CoolerFactory.Events.Rescind);
095: 
096:         Request storage req = requests[reqID];
097: 
098:         if (!req.active)
099:             revert Deactivated();
100:         
101:         req.active = false;

File: src/Cooler.sol
163:         Request storage req = requests[reqID];
164: 
165:         factory.newEvent(reqID, CoolerFactory.Events.Clear); 
166: 
167:         if (!req.active) 
168:             revert Deactivated();
169:         else req.active = false;
```

#### Impact
If factory is malicious it can reenter and manipulate the state of a request.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L81
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L94
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L165

#### Tool used
Manual Review

#### Recommendation
Move the `factory.newEvent` down in the functions, just before the transfer so the states are updated accordingly before the external call is made. Alternativls you can add a re-entrancy guard, but as the official factory has no possibility for a re-entrency attack it is not the best decision regarding gas effficiency.

### <a id="L07"></a> L-07 Missing important functions for protocol front-end
The current implementation for important state variables that the front-end need to consume miss some functions to work properly to display a lender all the possible Coolers he can choose to lend his tokens.
As statet in the [docs](https://ag0.gitbook.io/cooler-loan-code-walkthrough/borrower-flow) `we push the cooler address to our 'coolersFor' public mapping (used for front end purposes)` the front-end relys on the information what coolers exist for the collateral-debt combination.
With the current implementation the front-end can not display all the possible Coolers without extensive calls to the blockchain and also not the currently open Requests from a Cooler without extensive calls to the blockchain.

#### Vulnerability Detail

```solidity
File: src/Factory.sol
23:     mapping(ERC20 => mapping(ERC20 => address[])) public coolersFor;

File: src/Cooler.sol
20:     Request[] public requests;

File: src/Cooler.sol
29:     Loan[] public loans;
```
All three have access over an array but no helper function to get the length of the array. Also they have no "getBulk" for easier query.

#### Impact
The front-end can't get all the Coolers or all the Requests / Loans from a Caller in one call and also it's not possible for the front-end to know how many of them exist and how long it would take to get them all. So the front-end needs to start from index-0 and just query next, next until it gets an not exists.

#### Code Snippet
https://github.com/ohmzeus/Cooler/blob/main/src/Factory.sol#L23
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L20
https://github.com/ohmzeus/Cooler/blob/main/src/Cooler.sol#L29

#### Tool used
Manual Review

#### Recommendation
Add helper functions for the front-end like `collateralDebtLength` `requestsLength` `loansLength` and also maybe a bulk-fetch for them.

Example:

```solidity
function collateralDebtLength(address collateral, address debt) external returns (uint256) {
    return coolersFor[collateral][debt].length;
}

function getCoolersForCollateralDebt(address collateral, address debt, uint256 start, uint256 end) external returns ([]address memory _coolers) {
    // loop the coolersFor[collateral][debt] and return the coolers
}
```